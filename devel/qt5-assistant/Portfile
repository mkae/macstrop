# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           qmake5 1.0

name                qt5-assistant
set module          qttools
version             5.9.2

set branch          [join [lrange [split ${version} .] 0 1] .]

categories          devel aqua
description         A standalone version of Qt's Assistant.
long_description    Qt Assistant is the documentation browser from the QtTools component. \
                    Its full feature set and optimum rendering depends on the (revived) \
                    QtWebKit component.
universal_variant   no
installs_libs       no

maintainers         gmail.com:rjvbertin openmaintainer
platforms           darwin linux
license             LGPL-3

homepage            http://qt.io

subport ${name}-devel {
    description-append This port follows the development branch.
    long_description-append This port follows the development branch.
}

if {${subport} eq "${name}"} {
    distname        ${module}-opensource-src-${version}
    master_sites    http://download.qt.io/official_releases/qt/${branch}/${version}/submodules
    use_xz          yes
    conflicts-append \
                    ${name}-devel

    checksums       rmd160  42d6cdcaf62ca929d840ba8204acec0cc9b05bec \
                    sha256  2bb996118b68e9939c185a593837e5a41bb3667bf5d4d5134fac02598bd2d81a
} else {
    fetch.type      git
    git.url         git://code.qt.io/qt/${module}.git
    git.branch      041ae9c520a7c8ca2d359522460f796f2d8df1ad
#     v5.10.0-119-g041ae9c5
    version         5.10.0.119
    distname        ${module}-git
    conflicts-append \
                    ${name}
}

# a convenience procedure to register conflicts among the different Qt5 ports:
# for instance: [qt5_port_conflicts {qtbase x11}]
# adapted from port:qt5-kde
proc qt5_port_conflicts {components} {
    global available_qt5_versions available_qt_versions name
    set cnfls {}
    if {![info exists available_qt5_versions]} {
        ui_debug "set available_qt5_versions [array get available_qt_versions]"
        set available_qt5_versions [array get available_qt_versions]
    }
    foreach suffix {"" "-devel"} {
        foreach comp ${components} {
            foreach {v info} ${available_qt5_versions} {
                if {"${v}-${comp}" ne "${name}"} {
                    if {${comp} ne "" && ${comp} ne "qtbase"} {
                        set cnfls [lappend cnfls ${v}-${comp}]
                        set cnfls [lappend cnfls ${v}-kde${suffix}-${comp}]
                    } else {
                        set cnfls [lappend cnfls ${v}-qtbase]
                        set cnfls [lappend cnfls ${v}-kde${suffix}]
                    }
                }
            }
        }
    }
    return ${cnfls}
}

options                     extract_components
default extract_components  {}
proc collect_extract_args {} {
    foreach comp [option extract_components] {
        extract.post_args-append ${comp}
    }
}
pre-extract {
    collect_extract_args
}
extract.pre_args-append -T 0
platform linux {
    if {[file exists ${prefix}/bin/bsdtar]} {
        extract.post_args | ${prefix}/bin/bsdtar --no-same-owner -xf -
    }
}

if {[tbool qt5.using_kde]} {
    conflicts-append        [qt5_port_conflicts assistant]
    qt5.depends_component   webkit
} else {
    qt5.depends_component   qtwebkit
}

extract_components-append \
                        ${distname}/src/assistant ${distname}/src/shared

patch.pre_args          -Np1

patchfiles-append       patch-qttools-assistant-standalone-build.diff \
                        patch-assistant-fontpanel.diff

default configure.dir   {${workpath}/build}
configure.pre_args-append \
                        ../${worksrcdir}/src/assistant/assistant.pro
default build.dir       {${workpath}/build}

destroot {
    xinstall -m 755 -d ${destroot}${qt_bins_dir}
    xinstall -m 755 -d ${destroot}${qt_apps_dir}
    if {${os.platform} eq "darwin"} {
        file copy ${build.dir}/assistant/QtAssistant.app ${destroot}${qt_apps_dir}/
        if {[tbool qt5.using_kde]} {
            qt5.add_app_wrapper assistant-qt5 QtAssistant
        }
        file copy ${destroot}${prefix}/bin/assistant-qt5 ${destroot}${qt_bins_dir}/assistant
    } else {
        xinstall -m 755 ${build.dir}/assistant/assistant ${destroot}${qt_bins_dir}
        qt5.add_app_wrapper assistant-qt5 assistant assistant ${qt_bins_dir}
    }
}
