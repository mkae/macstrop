# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$
# Copyright (c) 2015, 2016 R.J.V. Bertin

PortSystem          1.0

PortGroup           kf5 1.1

name                kf5-translations
# this port isn't likely to evolve as fast as the actual software, so we hardcode the version
version             16.12.1
homepage            http://download.kde.org/stable/applications/${version}/src/kde-l10n/
master_sites        http://download.kde.org/stable/applications/${version}/src/kde-l10n/
maintainers         gmail.com:rjvbertin mk openmaintainer

installs_libs       no
supported_archs     noarch

description         Metaport providing KF5 translations
long_description    Metaport providing translations for KF5 applications that do not include their own translation files.

set languages { de en_GB fr nl }
foreach lang ${languages} {
    lappend mp.names "kf5-translations-${lang}"
}

set rmd160s     {
                    {a988edad4db5ddfc61328ce28842c288f33dd48e de}
                    {8ed0a9a3ce8c2f2117fa6fef99ed0c113e552239 en_GB}
                    {41cbf941185a3ec2a9828a3a652e4ea196df8349 fr}
                    {143b881be2a905fdfb09a4df5ee25d5b6b792380 nl}
                }
set sha256s     {
                    {4a629fbd5e3521c9860cbc88a836a4ffbfab7f081a52ee9398fbd4d0701d9b66 de}
                    {00a3b5591d81aae675df722bf26f7dff194e826c9a4058852c979628169fc671 en_GB}
                    {44ac1bcedee8151f01c372fa19edcb053f8b64f084ae00f9a4797fe91a3d5d1d fr}
                    {284fe46dadf67534880f97562bdfe210c4d976c4159f44e0727de7752c662128 nl}
                }

set kf5.allow_apidocs_generation no

if {${subport} eq ${name}} {
    foreach mp.name ${mp.names} {
        depends_build-append    port:${mp.name}
    }
    # use just any of the language tarballs as ours:
    distname        kde-l10n-en_GB-${version}
    fetch {}
    checksum {}
    extract {}
    configure {}
    build {}
    destroot {
        xinstall -d -m 755 ${destroot}${prefix}/share/doc/kf5-installed-metaports
        system "touch ${destroot}${prefix}/share/doc/kf5-installed-metaports/${name}"
    }
}

depends_build-append \
                    ${kf5::pythondep} \
                    port:gettext
kf5.depends_frameworks \
                    ki18n

foreach mp.name ${mp.names} {

    subport ${mp.name} {
        set idx [lsearch ${mp.names} ${mp.name}]
        set lang [lindex ${languages} $idx]
        if {${lang} eq "ca_valencia"} {
            set mp.name "kf5-translations-ca@valencia"
        }
        description         The \'${lang}\' translation files for KF5 applications
        long_description    ${description}
        kf5.require_kf5compat -port kde-l10n-${lang}
        distname            kde-l10n-${lang}-${version}
        worksrcdir          kde-l10n-${lang}-${version}
        checksums           rmd160  [lindex [lindex ${rmd160s} $idx] 0] \
                            sha256  [lindex [lindex ${sha256s} $idx] 0]
        # make sure the langselect variant is unset (if it exists)
        if {[variant_exists langselect]} {
            global variations
            set variations(langselect) -
        }
        if {![variant_isset docs]} {
            # the build system is still going to require KDoctools even if we don't build the documentation
            kf5.depends_frameworks \
                    kdoctools
            post-patch {
                reinplace -W ${worksrcpath} "s|add_subdirectory(docs)|# no documentation|g" 5/${lang}/CMakeLists.txt
            }
        }
        platform darwin {
            post-patch {
                reinplace -W ${worksrcpath} "s|add_subdirectory(data)|# no data files|g" 5/${lang}/CMakeLists.txt
                catch {system -W ${worksrcpath}/5/${lang}/messages "patch -Np0 -i ${filespath}/patch-only-applications.diff"} result context
            }
        }
        configure.args-append \
                    -DPYTHON_EXECUTABLE=${prefix}/bin/python${kf5::pyversion}
    }
}

extract.post_args-append \
                    ${worksrcdir}/CMakeLists.txt ${worksrcdir}/5
post-extract {
    if {[file exists ${worksrcpath}/CMakeLists.txt]} {
        reinplace "s|add_subdirectory(4)|# skipping KDE4 translations|g" ${worksrcpath}/CMakeLists.txt
    }
}

