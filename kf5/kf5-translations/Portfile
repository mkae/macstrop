# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$
# Copyright (c) 2015, 2016 R.J.V. Bertin

PortSystem          1.0

PortGroup           kf5 1.1

name                kf5-translations
# this port isn't likely to evolve as fast as the actual software, so we hardcode the version
version             16.08.2
homepage            http://download.kde.org/stable/applications/${version}/src/kde-l10n/
master_sites        http://download.kde.org/stable/applications/${version}/src/kde-l10n/
maintainers         gmail.com:rjvbertin mk openmaintainer

installs_libs       no
supported_archs     noarch

description         Metaport providing KF5 translations
long_description    Metaport providing translations for KF5 applications that do not include their own translation files.

set languages { de en_GB fr nl }
foreach lang ${languages} {
    lappend mp.names "kf5-translations-${lang}"
}

set rmd160s     {
                    {0e0763fc1560daa59054a4ad46b41d1fdbc323c3 de}
                    {189d371240dec1e74edc62b6f063c81633f65813 en_GB}
                    {29512f22eccde2fca864858edd190c6f308b7421 fr}
                    {ab3dfc7ab57d8274806044760e72b06e4e7ea715 nl}
                }
set sha256s     {
                    {344aac054d22da2e396ccd66ac40683e773d1fa1db9a142b6d231f473f826e06 de}
                    {4cbc74c058998e1bc865a0750895ea7b549225f9ea7651cee380d5793a2b8e89 en_GB}
                    {6353c72ff33287272b50ca40308b6bf79ccb029c9e2c7be279b72a4b3751691d fr}
                    {a44776a304611fc214279f824d3ed8d3746ab8194ebe8bde3460616b1e7e0743 nl}
                }

set kf5.allow_apidocs_generation no

if {${subport} eq ${name}} {
    foreach mp.name ${mp.names} {
        depends_build-append    port:${mp.name}
    }
    # use just any of the language tarballs as ours:
    distname        kde-l10n-en_GB-${version}
    fetch {}
    checksum {}
    extract {}
    configure {}
    build {}
    destroot {
        xinstall -d -m 755 ${destroot}${prefix}/share/doc/kf5-installed-metaports
        system "touch ${destroot}${prefix}/share/doc/kf5-installed-metaports/${name}"
    }
}

depends_build-append \
                    ${kf5::pythondep} \
                    port:gettext
kf5.depends_frameworks \
                    ki18n

foreach mp.name ${mp.names} {

    subport ${mp.name} {
        set idx [lsearch ${mp.names} ${mp.name}]
        set lang [lindex ${languages} $idx]
        if {${lang} eq "ca_valencia"} {
            set mp.name "kf5-translations-ca@valencia"
        }
        description         The \'${lang}\' translation files for KF5 applications
        long_description    ${description}
        conflicts-append    kde-l10n-${lang}
        distname            kde-l10n-${lang}-${version}
        worksrcdir          kde-l10n-${lang}-${version}
        checksums           rmd160  [lindex [lindex ${rmd160s} $idx] 0] \
                            sha256  [lindex [lindex ${sha256s} $idx] 0]
        # make sure the langselect variant is unset (if it exists)
        if {[variant_exists langselect]} {
            global variations
            set variations(langselect) -
        }
        if {![variant_isset docs]} {
            # the build system is still going to require KDoctools even if we don't build the documentation
            kf5.depends_frameworks \
                    kdoctools
            post-patch {
                reinplace -W ${worksrcpath} "s|add_subdirectory(docs)|# no documentation|g" 5/${lang}/CMakeLists.txt
            }
        }
        platform darwin {
            post-patch {
                reinplace -W ${worksrcpath} "s|add_subdirectory(data)|# no data files|g" 5/${lang}/CMakeLists.txt
                catch {system -W ${worksrcpath}/5/${lang}/messages "patch -Np0 -i ${filespath}/patch-only-applications.diff"} result context
            }
        }
        configure.args-append \
                    -DPYTHON_EXECUTABLE=${prefix}/bin/python${kf5::pyversion}
    }
}

extract.post_args-append \
                    ${worksrcdir}/CMakeLists.txt ${worksrcdir}/5
post-extract {
    if {[file exists ${worksrcpath}/CMakeLists.txt]} {
        reinplace "s|add_subdirectory(4)|# skipping KDE4 translations|g" ${worksrcpath}/CMakeLists.txt
    }
}

